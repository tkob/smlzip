# vim: set filetype=tcl :

# test for example/catzip/catzip

proc test_catzip {test_name zip_file_name entry_file_name} {
    global exampledir

    set temp_actual   [file tempfile temp_actual_file_name]
    set temp_expected [file tempfile temp_expected_file_name]
    try {
        if {[catch {exec $exampledir/catzip/catzip $zip_file_name $entry_file_name >@ $temp_actual} output]} {
            fail "$test_name execution failed: catzip\n$output\n"
            return
        }

        if {[catch {exec unzip -p $zip_file_name $entry_file_name >@ $temp_expected} output]} {
            fail "$test_name execution failed: zipinfo\n$output\n"
            return
        }

        spawn diff $temp_actual_file_name $temp_expected_file_name
        expect eof
        lassign [wait] process_id spawn_id os_err status

        if {$os_err} {
            fail "$test_name execution failed: diff\n"
            return
        }

        if {$status} {
            fail "$test_name failed\n"
            return
        } else {
            pass "$test_name passed.\n"
        }
    } finally {
        file delete $temp_actual_file_name
        file delete $temp_expected_file_name
    }
}

test_catzip "excel.xlsx" $fixturedir/excel.xlsx xl/workbook.xml
test_catzip "Java archive" $fixturedir/jar.jar META-INF/MANIFEST.MF
test_catzip "Java archive (uncompressed)" $fixturedir/jar0.jar META-INF/MANIFEST.MF

