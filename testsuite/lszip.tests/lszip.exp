# vim: set filetype=tcl :

# test for example/lszip/lszip

proc test_lszip {test_name zip_file_name} {
    global exampledir

    set temp_actual   [file tempfile temp_actual_file_name]
    set temp_expected [file tempfile temp_expected_file_name]
    try {
        if {[catch {exec $exampledir/lszip/lszip $zip_file_name >@ $temp_actual}]} {
            fail "$test_name execution failed: lszip\n"
            return
        }

        if {[catch {exec zipinfo -2 $zip_file_name >@ $temp_expected}]} {
            fail "$test_name execution failed: zipinfo\n"
            return
        }

        spawn diff $temp_actual_file_name $temp_expected_file_name
        expect eof
        lassign [wait] process_id spawn_id os_err status

        if {$os_err} {
            fail "$test_name execution failed: diff\n"
            return
        }

        if {$status} {
            fail "$test_name failed\n"
            return
        } else {
            pass "$test_name passed.\n"
        }
    } finally {
        file delete $temp_actual_file_name
        file delete $temp_expected_file_name
    }
}

test_lszip "excel.xlsx" $fixturedir/excel.xlsx

